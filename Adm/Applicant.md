---
title: Applicant
---

# 志願者・出願

志願者および志願者の出願データの基本構造をモデル化する.

## 依存モデル

```alloy
private open Base
private open Admission
private open Department

private open util/boolean
```

## 各種基本モデルおよび列挙値

通常はマスターあるいは列挙値として定義するもの.

### 受験番号

受験番号は受験者を特定するためのキーである.
試験の成績は受験番号に対してつけられ,合否の結果も同様に受験番号に対して付く.

```alloy
sig 受験番号{
}
```

## 志願者

志願者が表現するのは志願者個人である.
詳細設計においては氏名や性別,生年月日や住所などの個人情報を持たせるよう設計する必要がある.

```alloy
sig 志願者{
	年度 : 年度,
	期 : 入学期,
}
```

## 出願および出願詳細

出願の単位で受験番号を付与する.
また受験番号ごとに出欠と試験の成績を持つ.
ひとつの受験番号と成績で複数の入試募集への併願ができるため,複数の出願詳細を持つ.

```alloy
sig 出願{
	受験番号 : 受験番号,
	出願者: 志願者,
	出欠 : 出席区分,

	詳細 : some 出願詳細,
	試験成績 : set 成績,
}{
	詳細.~@詳細 in this
	試験成績.~@試験成績 in this
}

sig 成績{
	科目 : 科目,
	出欠 : 出席区分,
	素点 : Int,
}
```

出願詳細はひとつの入試募集への応募にあたる.
成績は出願詳細の対応する入試募集ごとの評価基準に照らして評価されるためそれぞれの評価結果を保持する.
評価の結果,同じ応募者の中で席次が付けられ,合否の判定が行われる.
また合格者については入学の手続き状況を保持する.

```alloy
sig 出願詳細{
	応募: 入試募集,
	希望順位: Int,
	合否 : 合否区分,
	手続き完了 : Bool,
	席次 : Int,

	試験評価 : set 評価,
}{
	試験評価.~@試験評価 in this
	pos[席次]
	pos[希望順位]
}

sig 評価{
	対象 : 入試募集,
	科目 : 科目,
	評価値 : Int,
}
```

## 前提

以下にモデルが有効であるための前提を示す.
この前提は運用を含めたシステム全体に対する仮定であり,次の正当性検証の前件となる.

```alloy
fact 志願者は年度をまたがって出願できない{
	all a: 志願者 |
		let n = a.出願している入試 |
			n.年度 = a.年度 and n.期 = a.期
}

-- 言い換えると評価は入試募集と成績だけで決定される
fact どんな入試募集でも同じ成績なら評価は同じになる{
	all r: 入試募集 |
		some x,x': 出願 |
			x.試験成績 = x'.試験成績 => 出願詳細[x, r].試験評価 = 出願詳細[x', r].試験評価
}

fact 受験番号は年度で一意とする{
	all disj x,x': 出願 |
		同一年度期である[x.入試募集.~募集, x'.入試募集.~募集] => x.受験番号 != x'.受験番号
}

fact 志願者が手続き完了できる出願詳細はたかだか一つだけ{
	all a: 志願者 |
		lone {d: a.出願詳細 | d.手続き完了 = True}
}
```

## 仕様および基本設計の正当性検証

上記モデルの満足すべき仕様を以下に示す.
これらの検証により基本設計としてのモデルが正当であることを保証する.


```alloy
assert 志願者に紐付かない出願は存在しない{
	no a: 出願 |
		no a.出願者
}
check 志願者に紐付かない出願は存在しない

assert 出願詳細のない出願は存在しない{
	no a: 出願 |
		no a.詳細
}
check 出願詳細のない出願は存在しない

assert 出願詳細が共有されることはない{
	no d: 出願詳細 |
		some disj a,a': 出願 |
			d in a.詳細 and d in a'.詳細
}
check 出願詳細が共有されることはない
```

```alloy
pred 志願者は複数の出願ができる{
	some a: 志願者 |
		#a.~出願者 > 1
}
run 志願者は複数の出願ができる

pred 志願者はひとつの出願で複数の入試募集に同時併願できる{
	some a: 志願者, a': a.~出願者 |
		#a'.詳細 > 1
}
run 志願者はひとつの出願で複数の入試募集に同時併願できる

-- 明治なんかはこれができる
pred 同じ入試に複数の出願ができる{
	some a: 志願者 |
		some disj x,x': a.~出願者 |
			x.入試募集.~募集 = x'.入試募集.~募集
}
run 同じ入試に複数の出願ができる

pred 同じ受験番号の成績でも募集ごとに評価が異なる{
	some a: 出願 |
		some disj d,d': a.詳細 |
			some d.試験評価 and
			some d'.試験評価 and
			d.試験評価 != d'.試験評価
}
run 同じ受験番号の成績でも募集ごとに評価が異なる

pred 受験番号ごとに出席を取れる{
	all n: this/受験番号 |
		one n.~受験番号.出欠
}
run 受験番号ごとに出席を取れる

pred 受験科目ごとに出席を取れる{
	all s: 成績 |
		one s.出欠
}
run 受験科目ごとに出席を取れる

pred 志願者は複数の学部学科に合格できる{
	some a: 志願者 |
		#合格している出願詳細[a].募集学科 > 1
}
run 志願者は複数の学部学科に合格できる

pred 志願者を入学手続き完了にできる{
	some a: 志願者 |
		入学手続き完了している[a]
}
run 志願者を入学手続き完了にできる
```

## ユーティリティ

```alloy
fun 出願詳細(a: 志願者) : set 出願詳細{
	a.~出願者.詳細
}
fun 出願している入試募集(a: 志願者) : set 入試募集{
	a.出願詳細.応募
}
fun 募集学科(a: 出願詳細) : 学部学科{
	a.応募.募集学科
}
fun 出願している入試(a: 志願者) : set 入試{
	a.出願している入試募集.~募集
}
fun 入試募集(a: 出願) : set 入試募集{
	a.詳細.応募
}
fun 出願詳細(a: 出願, r: 入試募集) : 出願詳細{
	{d: a.詳細 | d.応募 = r}
}
fun 合格している出願詳細(a: 志願者) : set 出願詳細{
	{d: a.出願詳細 | d.合否 in 合格}
}
pred 入学手続き完了している(a: 志願者){
	a.出願詳細.手続き完了 = True
}
```
