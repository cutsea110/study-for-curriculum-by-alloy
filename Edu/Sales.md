---
title: Sales
---

# 学費の概要

学費は学生を顧客とみなして,大学における主要な売上となる.
入学金や授業料などの売上を立てるとともに請求および入金の処理を表現する.

## 依存モデル

```alloy
private open Base
private open Department
private open Student as G

private open util/time
```

## 各種列挙値

上位のモデルで使用する列挙値を定義する.

```alloy
enum 特待生 { 特S, 特A, 特B, 特C }
sig 勘定科目{
}
sig 費目{
	科目コード : 勘定科目,
	入金消込順位 : Int,
}
```

## 顧客

学生を顧客とみなす.

```alloy
sig 顧客 extends 学生{
	特待区分 : lone 特待生,

	請求先 : lone 関係者,
}
```

## 売上

各学生からの学費が売上あるいは売掛となる.

```alloy
sig 売上{
	売上日 : Time,
	顧客 : 顧客,

	年度 : 年度,
	期 : 期,
}

sig 売上明細{
	売上 : 売上,

	種別 : 費目,
	売上額 : Int,
	減免額 : Int,
	充当額 : Int,
	初回請求日 : lone Time,
	最終請求日 : lone Time,
	消込日 : lone Time,
}
```

## 請求

学費の請求は各顧客学生の請求先に年度,期ごとにまとめて行えるように定義する.

```alloy
sig 請求{
	請求日 : Time,
	顧客 : 顧客,
	請求先 : 関係者, -- 顧客(学生)の請求先
}

sig 請求明細{
	請求 : 請求,

	対象 : 売上明細,
	請求額 : Int,
	減免額 : Int,
	充当額 : Int,
	消込日 : lone Time,
}
```

## 入金

入金はあくまで銀行のEBなどからの入金として定義する.
不明入金であっても入金データは生成する.
不明入金がありえるので振込元や顧客は必ず特定できるとは保証できない.

```alloy
sig 入金口座{
}

sig 入金{
	入金日 : Time,
	振込元 : lone 関係者,
	入金額 : Int,
	入金口座 : 入金口座,

	顧客 : lone 顧客,
	充当額 : Int,
	消込日 : lone Time,
}
```

## 学費

学費は商品マスタに相当し,売上を自動で生成するためのテンプレートとなる.
費目は会計上の勘定科目コードに対応する.

学費基本は入学年度および期,学生種別と特待生区分で決まる部分の学費を定義する.
年度や所属学科や年次に依存していない学費を表す.
ある程度売上を自動で生成するための仕掛けに過ぎないことに注意.

```alloy
sig 学費基本{
	入学年度 : 年度,
	入学期 : 期,

	学生区分 : 学生区分,
	特待区分 : lone 特待生,

	費目 : 費目,
	金額 : Int,

	追加 : 学費オプション,
}
```

学費オプションは年度および期,学生の所属学科と年次や留年区分などによって変わる部分の学費を定義する.
ある程度売上を自動で生成するための仕掛けに過ぎないことに注意.

```alloy
enum 対象指定方式 { 学科と年次が合致, 学科が合致する留年生 }

sig 学費オプション{
	対象指定 : 対象指定方式,
	学科 : 学科,
	学年 : 年次,

	金額 : Int,
}
```

## 支払い計画と学費納付計画

支払い計画は売上から請求書を生成する際に一括か分納かなどの支払いのパターンを定義する.
学費納付計画は年度,期ごと学生ごとにどの支払いパターンを利用するかを表す.

```alloy
sig 支払い計画{
	年度 : 年度,
	期 : 期,
	計画 : set 支払い,
}
sig 支払い{
	請求割合 : Int,
	請求残全額 : Bool,
	納付期限 : Time,
}
sig 学費納付計画{
	年度 : 年度,
	期 : 期,
	学生 : 顧客,

	計画 : 支払い計画,
}
```

## 前提

以下にモデルが有効であるための前提を示す.
この前提は運用を含めたシステム全体に対する仮定であり,次の正当性検証の前件となる.

```alloy
fact 充当額は売上額以下である{
	all d: this/売上明細 |
		d.売上額 >= d.充当額
}
fact 充当額が売上額と同じなら入金済である{
	all d: this/売上明細 |
		d.売上額 = d.充当額 => d.入金済
}
fact 充当額は請求額以下である{
	all d: this/請求明細 |
		d.請求額 >= d.充当額
}
fact 充当額が請求額と同じなら入金済である{
	all d: this/請求明細 |
		d.請求額 = d.充当額 => d.入金済
}
```

## 仕様および基本設計の正当性検証

上記モデルの満足すべき仕様を以下に示す.
これらの検証により基本設計としてのモデルが正当であることを保証する.

```alloy
assert 異なる売上に含まれる売上明細はない{
	no d: this/売上明細 |
		some disj s, s': this/売上 |
			d in s.売上明細 and d in s'.売上明細
}
check 異なる売上に含まれる売上明細はない
assert 異なる請求に含まれる請求明細はない{
	no d: this/請求明細 |
		some disj r,r': this/請求 |
			d in r.請求明細 and d in r'.請求明細
}
check 異なる請求に含まれる請求明細はない

pred 年度一括で売上を立てることができる{
	some {s: this/売上 | s.期 = 通期}
}
run 年度一括で売上を立てることができる

pred 期ごとに売上を立てることができる{
	some {s: this/売上 | s.期 in (前期 + 後期)}
}
run 期ごとに売上を立てることができる

pred 売上に対して分割請求することができる{
	some disj i,i': this/請求 |
		let s  = i.請求明細.対象売上, s' = i'.請求明細.対象売上 |
			some s and some s' and s = s'
}
run 売上に対して分割請求することができる

pred 学生に対して学費納付計画を立てることができる{
	some g: G/学生 |
		some g.~(学費納付計画 <: 学生)
}
run 学生に対して学費納付計画を立てることができる

pred 学費の支払い計画として分納を計画することができる{
	some p: 支払い計画 |
		#p.計画 > 1 and #p.計画.納付期限 > 1
}
run 学費の支払い計画として分納を計画することができる

pred 学費がなくても売上がつくれる{
	some s: this/売上 |
		some s.売上明細 and no this/学費基本 and no this/学費オプション
}
run 学費がなくても売上がつくれる
pred 学費納付計画がなくても請求書をつくれる{
	some i: this/請求 |
		some i.請求明細 and no this/学費納付計画
}
run 学費納付計画がなくても請求書をつくれる
```

## ユーティリティ

```alloy
pred 請求済(d: this/売上明細){
	some d.最終請求日
}
pred 全て請求済(s: this/売上){
	all d: s.売上明細 |
		d.請求済
}
pred 入金済(d: this/請求明細){
	some d.消込日
}
pred 全て入金済(i: this/請求){
	all d: i.請求明細 |
		d.入金済
}
pred 入金済(d: this/売上明細){
	some d.消込日
}
pred 全て入金済(s: this/売上){
	all d: s.売上明細 |
		d.入金済
}

fun 請求明細(i: this/請求) : set 請求明細{
	i.~請求
}
fun 対象売上(d: this/請求明細) : set this/売上{
	d.対象.売上
}
fun 売上明細(s: this/売上) : set 売上明細{
	s.~売上
}
```

