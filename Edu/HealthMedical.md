---
title: HealthMedical
---

# 学生健康診断の概要

学生の健康診断データを定義する.

## 依存モデル

```alloy
private open Base as B
private open Student
```

## 各種列挙値

上位のモデルで使用する列挙値を定義する.

```alloy
private enum 検診種別 { 胸部, 視力, 聴力, 身体, 糖尿, 検査 }
```

## 健康診断

学生の健康診断は年度ごとに複数の種別の検診データを複数回分保持できるようにモデル化する.

```alloy
sig 学生健康診断{
	対象 : 学生,
	年度 : 年度,
	検診日 : Time,
	医師名 : 医師,
	発行可 : Bool,
}

abstract sig 検診{
	対象 : 学生,
	年度 : 年度,
	検査日 : Time,
	種別 : 検診種別,
}

sig 胸部検診 extends 検診{
}{
	種別 = 胸部
}

sig 視力検診 extends 検診{
}{
	種別 = 視力
}

sig 聴力検診 extends 検診{
}{
	種別 = 聴力
}

sig 身体検診 extends 検診{
}{
	種別 = 身体
}

sig 糖尿検診 extends 検診{
}{
	種別 = 糖尿
}

sig 検査検診 extends 検診{
}{
	種別 = 検査
}

private sig 医師{
}
```

## 仕様および基本設計の正当性検証

上記モデルの満足すべき仕様を以下に示す.
これらの検証により基本設計としてのモデルが正当であることを保証する.

```alloy
pred 学生ごとに健康診断データを管理できる{
	some disj x,x': 学生健康診断 |
		x.対象 != x'.対象
}
run 学生ごとに健康診断データを管理できる

pred 学生は年度ごとに健康診断データを保持できる{
	some disj x,x': 学生健康診断 |
		x.対象 = x'.対象 and x.年度 != x'.年度
}
run 学生は年度ごとに健康診断データを保持できる

pred 学生は同一年度に複数の健康診断データを保持できる{
	some disj x,x': 学生健康診断 |
		x.対象 = x'.対象 and x.年度 = x'.年度
}
run 学生は同一年度に複数の健康診断データを保持できる

pred 学生は年度ごとに検診データを保持できる{
	some disj x,x': 検診 |
		x.種別 = x'.種別 and x.対象 = x'.対象 and x.年度 != x'.年度
}
run 学生は年度ごとに検診データを保持できる

pred 学生は同一年度に同じ種類の検診データを複数保持できる{
	some disj x,x': 検診 |
		x.種別 = x'.種別 and x.対象 = x'.対象 and x.年度 = x'.年度
}
run 学生は同一年度に同じ種類の検診データを複数保持できる
```
